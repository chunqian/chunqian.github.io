# 6.7.迭代器

Haxe 可以很方便的定义定制的迭代器与可迭代数据类型，其概念分别由 `Iterator<T>` 与 `Iterable<T>` 表示：

> With Haxe it is very easy to define custom iterators and iterable data types. These concepts are represented by the types `Iterator<T>` and `Iterable<T>` respectively:

```haxe
typedef Iterator<T> = { 
    function hasNext() : Bool; 
    function next() : T; 
} 

typedef Iterable<T> = { 
    function iterator() : Iterator<T>; 
} 
```

任何可以与这两个结构统一的类型都可以被 for 循环迭代。也就是说，如果一个类中定义了 `hasNext()` 与 `next()` 方法并匹配到相应的返回类型，那么该类被认为是一个迭代器 ，而如果一个类中定义了 `iterator()` 方法并返回一个 `Iterator<T>` ，那么该类被认为是一个可迭代的类型。

> Any [class](https://haxe.org/manual/types-class-instance.html) which [structurally unifies](https://haxe.org/manual/type-system-structural-subtyping.html) with one of these types can be iterated over using a [for-loop](https://haxe.org/manual/expression-for.html). That is, if the class defines methods `hasNext` and `next` with matching return types it is considered an iterator, if it defines a method `iterator` returning an `Iterator<T>` it is considered an iterable type.

```haxe
class MyStringIterator { 
    var s:String; 
    var i:Int; 
    
    public function new(s:String) { 
        this.s = s;
        i = 0; 
    } 
    
    public function hasNext() { 
        return i < s.length;
    } 
    
    public function next() { 
        return s.charAt(i++); 
    } 
} 

class Main { 
    static public function main() { 
        var myIt = new MyStringIterator("string"); 
        for (chr in myIt) { 
            trace(chr); 
        } 
    }
} 
```

本例中的类型 `MyStringIterator` 有资格作为一个迭代器：它定义了一个方法 `hasNext()`，并返回一个 `Bool` 值，以及另一个方法 `next()`，并返回一个 `String` 值，使得它可与 `Iterable<String>` 兼容。 `main` 中对它进行了初始化之后对其进行了迭代。

> The type `MyStringIterator` in this example qualifies as iterator: It defines a method `hasNext` returning `Bool` and a method `next` returning `String`, making it compatible with `Iterator<String>`. The `main`method instantiates it, then iterates over it.

```haxe
class MyArrayWrap<T> { 
    var a:Array<T>;
    public function new(a:Array<T>) { 
        this.a = a; 
    } 
    
    public function iterator() { 
        return a.iterator(); 
    } 
} 

class Main { 
    static public function main() { 
        var myWrap = new MyArrayWrap([1, 2, 3]); 
        for (elt in myWrap) {
            trace(elt); 
        } 
    } 
} 
```

这里我们没有如上一个例子那样建立一整个迭代器，而是定义了一个 `MyArrayWrap<T>` 类型，并为其定义了一个 `iterator()` 方法，以此方式高效地递交包装过的 `Array<T>` 类型的迭代工作。

> Here we do not setup a full iterator like in the previous example, but instead define that the `MyArrayWrap<T>` has a method `iterator`, effectively forwarding the iterator method of the wrapped `Array<T>` type.



##### [since Haxe 4.0.0](https://haxe.org/manual/lf-iterators.html#since-haxe-4.0.0)

标准库还包含了键值对迭代器，以供对 [键值对迭代](https://haxe.org/manual/expression-for.html#key-value-iteration) 的支持。

> The standard library also includes key-value iterators to support [key-value iteration](https://haxe.org/manual/expression-for.html#key-value-iteration):

```haxe
typedef KeyValueIterator<K, V> = {
  function hasNext():Bool;
  function next():{key:K, value:V};
}

typedef KeyValueIterable<K, V> = {
  function keyValueIterator():KeyValueIterator<K, V>;
}
```



##### [Related content](https://haxe.org/manual/lf-iterators.html#related-content)

- See the [Iterator API documentation](http://api.haxe.org/Iterator.html).
- [Haxe snippets and tutorials about iterators](http://code.haxe.org/tag/iterator.html) in the Haxe Code Cookbook.